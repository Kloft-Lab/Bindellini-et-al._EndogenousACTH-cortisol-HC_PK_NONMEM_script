;; 1. Based on: 
;; 2. Description: Endogenous ACTH/cortisol + HC PK - Joint Model
;; x1. Author: DBi


$PROBLEM    PD-model ACTH-CORT + PK-CORT
$INPUT      ID TIM DROP=CLTD DROP=DAT AMT RATE LINDV DV EVID MDV CMT FLAG TPER TOT DVID FORM BLQ CBG ALB BW HT AGE DOSE TIME TIME3 DROP=DAY ICL IV2 IQ IV3 IKD INS STUD DOSE2 TAD DOSETR EXCL

$DATA       KP-HCP07_INF001002_20230123_CT.csv IGNORE=@ IGNORE(DVID.EQ.2) IGNORE(FLAG.EQ.5) IGNORE(FLAG.EQ.7) IGNORE(EXCL.EQ.1) IGNORE(BLQ.EQ.1)
$SUBROUTINE ADVAN13 TOL=12
$MODEL      NCOMP=4 COMP=(DEPOT) COMP=(ACTH) COMP=(CORT) COMP=(CORTP) 
$PK 
"FIRST
" USE PRDATA, ONLY: MXSTP01
" MXSTP01=2147483647 
  
;------------------------ ABSORPTION ------------------
DOSETRR=DOSETR
IF(TAD.EQ.0.AND.TIM.EQ.0) THEN
DOSETRR=0
ENDIF

KA=THETA(25)*EXP(ETA(9))

VAR=0
IF(FLAG.EQ.1) THEN
VAR=ETA(18)
ENDIF
IF(FLAG.EQ.2) THEN
VAR=ETA(19)
ENDIF
IF(FLAG.EQ.3) THEN
VAR=ETA(20)
ENDIF
IF(FLAG.EQ.4) THEN
VAR=ETA(21)
ENDIF
IF(FLAG.EQ.6) THEN
VAR=ETA(22)
ENDIF

IF(DOSE.NE.0.AND.FLAG.NE.10)THEN
DOSECOV=DOSE
ENDIF

MTT=THETA(26)*(DOSECOV/5)**THETA(36)*EXP(VAR)
NN=THETA(34)*EXP(ETA(10))
KTR=(NN+1)/MTT

F1=0
TVBIO=THETA(27)
LOGITBIO=LOG(TVBIO/(1-TVBIO))
BIO=EXP(LOGITBIO+ETA(11))/(1+EXP(LOGITBIO+ETA(11)))    

LNFAC=LOG(2.5066)+(NN+0.5)*LOG(NN)-NN  
;NFAC =SQRT(2*3.1415)*NN**(NN+0.5)*EXP(-NN) 

;---------------------------ACTH SURGES-----------------
SA1=THETA(1)*((BW/70)**THETA(37))*EXP(ETA(1))
SW1=THETA(2)
Pt=THETA(3)*EXP(ETA(6))

SA2=THETA(4)
SW2=THETA(20)
Pt2=THETA(5)

;---------------- EFFECT PART - INHIB OF ACTH ---------
KOUT=THETA(6)*EXP(ETA(2))
IMAX=THETA(7)
IC50=THETA(8)*EXP(ETA(3))
GAMMAI=THETA(22)
IMAX_BASE=THETA(23)

BASE=THETA(9)*EXP(ETA(7))

;---------------- CORT SECRETION (PD) -----------------
EMAX =THETA(10)*EXP(ETA(4))
EC50  =THETA(11)*EXP(ETA(8))
GAMMAE=THETA(12)

IF(STUD.EQ.1) THEN ; No endogenous data from study 1 --> invidivual ETAs estimation not supported
SA1=THETA(1)*((BW/81.75)**THETA(37))
KOUT=THETA(6)
BASE =THETA(9)
EC50  =THETA(11)
ENDIF

KIN  =BASE*KOUT 

;-------------- CORT DISPOSITION ----------------------
CL = THETA(28)*(BW/70)**0.75*EXP(ETA(12))
V2 = THETA(29)*(BW/70)*EXP(ETA(13))
Q = THETA(30)*(BW/70)**0.75*EXP(ETA(14))
V3 = THETA(31)*(BW/70)*EXP(ETA(15))

k20=CL/V2
k23=Q/V2
k32=Q/V3

;--------------- BINDING MODEL ------------------------
;CBGmol=CBG*1000/52 ; Converting CBG from ug/mL to nmol/L. Mw=52000g/mol
CBG2=CBG            ;CBG given in dataset for study 2.
IF(CBG.EQ.-99) THEN
CBG2=22.4 ; CBG is sampled from the distribution for study 1.
ENDIF
CBGmol=CBG2*1000/52

BMAX=CBGmol*EXP(LOG(THETA(13))+ETA(5)) ; Bmax=CBG concentration * number of binding sites at CBG (fixed to 1)
AMAX=BMAX*V2

KD= IKD
;KD = THETA(32)*EXP(ETA(16))
K=KD*V2

NS= INS ; Nonspecific binding parameter
;NS = THETA(33)*EXP(ETA(17))

; ------------- INITIALISE CMTs -----------------------
A_0(2)=BASE
IBASE=THETA(35)*EXP(ETA(23))
A_0(3)=IBASE

;-------------- DEX Inihibition -----------------------
IF(FLAG.EQ.20) THEN
IDEX=1
IDEX_BASE=1
ENDIF

IF(FLAG.NE.20)THEN
IDEX=1-(THETA(21)/10000)
IDEX_BASE=1-(THETA(24)/10000)
ENDIF

;-------------- DAY - repeat surges -------------------
DAY=-1
IF(TIME.GE.9.AND.TIME.LT.33) THEN
DAY=0
ENDIF
IF(TIME.GE.33.AND.TIME.LT.57) THEN 
DAY=1
ENDIF
IF(TIME.GE.57.AND.TIME.LT.81) THEN
DAY=2
ENDIF 

;--------------DIFFERENTIAL EQUATIONS------------------
$DES 
SUR1=SA1/((ABS(T-(Pt+(24*DAY)))/SW1)**4+1)
SUR2=SA2/((ABS(T-(Pt2+(24*DAY)))/SW2)**4+1)

AU = (A(3)-K*(1+NS)-AMAX+SQRT(((A(3)-K*(1+NS)-AMAX)**2)+4*K*A(3)*(1+NS)))/(2*(1+NS)) 
FL=1-((IMAX/10000)*((AU/V2)**GAMMAI)/((AU/V2)**GAMMAI+IC50**GAMMAI)) ; Feedback loop 

DADT(1)=EXP(LOG(BIO*DOSETRR+.00001)+LOG(KTR)+NN*LOG(KTR*TAD+.00001)-KTR*TAD-LNFAC)-KA*A(1) 
DADT(2)=KIN+((SUR1+SUR2)*IDEX*FL)-KOUT*A(2)

EACTH=EMAX*A(2)**GAMMAE/(EC50**GAMMAE+A(2)**GAMMAE)
DADT(3)=EACTH-(k20+k23)*AU+k32*A(4)+KA*A(1)
DADT(4)=-k32*A(4)+k23*AU


$ERROR
AU2 = (A(3)-K*(1+NS)-AMAX+SQRT(((A(3)-K*(1+NS)-AMAX)**2)+4*K*A(3)*(1+NS)))/(2*(1+NS)) 

IF(CMT.EQ.2) THEN
IPRED=A(2)
W=SQRT(THETA(15)**2+(THETA(14)**2/IPRED**2))
ENDIF
IF(CMT.EQ.3.AND.TOT.EQ.1) THEN
IPRED=A(3)/V2
W=SQRT(THETA(17)**2+(THETA(16)**2/IPRED**2))
ENDIF
IF(CMT.EQ.3.AND.TOT.EQ.0) THEN
IPRED=AU2/V2
W=SQRT(THETA(19)**2+(THETA(18)**2/IPRED**2))
ENDIF

IF(IPRED.GT.0) THEN 
IPRED=LOG(IPRED)
ELSE
IPRED=LOG(IPRED+0.01)
ENDIF

IRES=DV-IPRED
IWRES=IRES/W
Y=IPRED+W*EPS(1)

$THETA
(0, 1300) ; SA1
(0.606) ; SW
(0, 15.3,24) ; Pt
(0, 50) ; SA2
(0, 20.8,24) ; Pt2
(0, 0.613) ; Kout
(9990) FIX ; IMAX
(0, 4.6) ; IC50
(0, 1.29) ; BASE
(0, 5400) ; EMAX
(0, 6.63) ; EC50
(2.94) ; GAMMAeff
(1) FIX ; NCBG
(0) FIX ; ADD_ERR_ACTH
(0, 0.252) ; PROP_ERR_ACTH
(0) FIX ; ADD_ERR_CORT
(0, 0.146) ; PROP_ERR_CORT
(0, 1.38) ; ADD_ERR_UNBCORT
(0, 0.267) ; PROP_ERR_UNBCORT
(0, 2.33) ; SW2
(10000) FIX ; IDEX
(0, 5.33) ; GAMMAinh 
(0) FIX ; IMAX_BASE
(0) FIX ; IDEX_BASE
(0, 24) ; KA [nmol/h]
(0, 0.868) ; MTT
(0, 0.344,1) ; F1 [-]
(0, 106) ; 1. CL [L/h]
(0.001, 2.15) ; 2. V2 [L]
(0, 89.9) ; 3. Q [L/h]
(0.01, 61.7) ; 4. V3 [L] 
(9.71) FIX ; 9. KD [nmol/L], fixed from binding model
(4.15) FIX ; 10. NS [-], fixed from binding model
(0, 2.12) ; NN
(0, 15.7) FIX ; Cort base
(0.179) ; expMTTdose
(0, 6.53) ; SA1BW

$OMEGA
 0.184 ; IIV_SA-SW
 0.253 ; IIV_Kout
 0 FIX  ; IIV_IC50
 0 FIX  ; IIV_EMAX
 0.049 FIX  ; IIV_NCBG
 0 FIX  ; IIV_Pt
$OMEGA BLOCK(2)
 0.0589  ;  IIV_BASE
 0.0626 0.0736 ;  IIV_EC50
$OMEGA
 0 FIX  ; IIV_KA
 0.17 ; IIV_NN
 0.207 ; IIV_F 
 0.013 ; IIV_CL
 0 FIX  ; IIV_V2
 0 FIX  ; IIV_Q
 0.0148 ; IIV_V3
 0 FIX  ; IIV_KD
 0 FIX  ; IIV_NS   
$OMEGA BLOCK(1) 0.0787 ; IOV_MTT
$OMEGA BLOCK (1) SAME ; VAR 2
$OMEGA BLOCK (1) SAME ; VAR 5
$OMEGA BLOCK (1) SAME ; VAR 10
$OMEGA BLOCK (1) SAME ; VAR 20 
$OMEGA
 0.091 FIX  ; IIV_Base

$SIGMA 1 FIX  ; SIGMA    

; $SIM (12345) (54321) ONLYSIM NSUB=1000
$ESTIMATION MAX=9999 PRINT=1 MSFO=run118.msf METH=1 INTER NOABORT SIGDIG=3
$COVARIANCE FPOSDEF=1 MATRIX=R PRINT=E UNCONDITIONAL

$TABLE ID TIME AMT RATE DV LINDV EVID MDV CMT FLAG TPER TOT DVID FORM BLQ CBG ALB BW HT AGE DOSE IPRED PRED CL V2 Q V3 KD NS IDEX IDEX_BASE EC50 IC50 AU BASE ICL IV2 IQ IV3 IKD INS BMAX F1 BIO NN KA KTR MTT VAR
ETA1 ETA2 ETA3 ETA4 ETA5 ETA6 ETA7 ETA8 ETA9 ETA10 ETA11 ETA12 ETA13 ETA14 ETA15 ETA16 ETA17 ETA18 ETA19 ETA20 ETA21 ETA22 CWRES NOPRINT NOAPPEND ONEHEADER FILE=sdtab118
